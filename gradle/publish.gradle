apply plugin: 'maven-publish'
apply plugin: 'signing'

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'jetcache-plus'
                description = 'Enhancements to JetCache'
                url = 'https://github.com/qy8502/jetcache-plus'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/qy8502/jetcache-plus/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'qy8502'
                        name = 'Qu Yankai'
                        email = 'qy8502@163.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:qy8502/jetcache-plus.git'
                    developerConnection = 'scm:git:ssh://github.com/qy8502/jetcache-plus.git'
                    url = 'https://github.com/qy8502/jetcache-plus'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}